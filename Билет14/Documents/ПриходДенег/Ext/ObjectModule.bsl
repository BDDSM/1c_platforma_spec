
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ДолгиПоДокументам.Записывать = Истина;
	Движения.ДолгиПоДокументам.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДолгиПоДокументам");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДолгиПоДокументамОстатки.Документ КАК Документ,
		|	ДолгиПоДокументамОстатки.Валюта КАК Валюта,
		|	ПРЕДСТАВЛЕНИЕ(ДолгиПоДокументамОстатки.Валюта) КАК ВалютаПредставление,
		|	ДолгиПоДокументамОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс
		|ИЗ
		|	РегистрНакопления.ДолгиПоДокументам.Остатки(&МоментВремени, Контрагент = &Контрагент И Документ <> ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)) КАК ДолгиПоДокументамОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютСрезПоследних
		|		ПО ДолгиПоДокументамОстатки.Документ.Валюта = КурсыВалютСрезПоследних.Валюта
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДолгиПоДокументамОстатки.Документ.МоментВремени";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СуммаКРаспределению = СуммаПоДокументу;
	
	Пока СуммаКРаспределению > 0 И ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписи.Курс = 0 Тогда 
			
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не найден курс для валюты: %1", ВыборкаДетальныеЗаписи.ВалютаПредставление);
			Сообщение.Сообщить();
			
		КонецЕсли;
		
		Если Отказ Тогда
		
			Продолжить;
		
		КонецЕсли;
		
		СуммаДолгаРубли = Окр(ВыборкаДетальныеЗаписи.СуммаОстаток * ВыборкаДетальныеЗаписи.Курс, 2); 
		
		СуммаКСписаниюРубли  = МИН(СуммаДолгаРубли, СуммаКРаспределению);
		
		СуммаКСписаниюВалюта = ?(СуммаДолгаРубли = СуммаКСписаниюРубли, 
			ВыборкаДетальныеЗаписи.СуммаОстаток,
			Окр(СуммаКСписаниюРубли / ВыборкаДетальныеЗаписи.Курс, 2));
		
		Движение = Движения.ДолгиПоДокументам.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Документ = ВыборкаДетальныеЗаписи.Документ;
		Движение.Валюта = ВыборкаДетальныеЗаписи.Валюта;
		Движение.Сумма = СуммаКСписаниюВалюта;
		
		СуммаКРаспределению = СуммаКРаспределению - СуммаКСписаниюРубли;
		
	КонецЦикла;
	
	Если НЕ Отказ И СуммаКРаспределению > 0 Тогда
		Движение = Движения.ДолгиПоДокументам.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
		Движение.Сумма = СуммаКРаспределению;
	КонецЕсли;

	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	
КонецПроцедуры
