
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// в условии 16 задачи противоречащие условия: "Отгрузка товара осуществляется по предоплате" и 
	// "При поступлении денег гасятся задолженности по накладным, начиная с 
	//		самой первой недоплаченной накладной. Авансов и переплат быть не может."
	// т.к. уточнить не у кого - решаю, исходя из предположения, что переплаты быть не может 
	//
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Взаиморасчеты.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВзаиморасчетыОстатки.Документ КАК Документ,
	|	ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.Взаиморасчеты.Остатки(&МоментВремени, Контрагент = &Контрагент) КАК ВзаиморасчетыОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВзаиморасчетыОстатки.Документ.МоментВремени
	|ИТОГИ
	|	СУММА(СуммаОстаток)
	|ПО
	|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщийИтог.Следующий();
	
	Если ВыборкаОбщийИтог.СуммаОстаток = NULL Тогда
		
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нет накладных для зачета оплаты!";
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если СуммаПоДокументу > ВыборкаОбщийИтог.СуммаОстаток Тогда
		
		НеХватает = СуммаПоДокументу - ВыборкаОбщийИтог.СуммаОстаток;
		
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Долг контрагент меньше указанной суммы! Сумма долга: %1", ВыборкаОбщийИтог.СуммаОстаток);
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = ВыборкаОбщийИтог.Выбрать();
	
	ИтогоСписать = СуммаПоДокументу;
	Пока ИтогоСписать > 0 И ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СуммаСписать = МИН(ИтогоСписать, ВыборкаДетальныеЗаписи.СуммаОстаток);
		ИтогоСписать = ИтогоСписать - СуммаСписать;
		
		Движение = Движения.Взаиморасчеты.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Документ = ВыборкаДетальныеЗаписи.Документ;
		Движение.Сумма = СуммаСписать;
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

	
	
	
	// регистр Взаиморасчеты Расход
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	
КонецПроцедуры
