
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ПериодРегистрации = НачалоМесяца(ПериодРегистрации);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОсновныеНачисления.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение,
		|	НачислениеЗарплатыОсновныеНачисления.ГрафикРаботы КАК ГрафикРаботы,
		|	НачислениеЗарплатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	НачислениеЗарплатыОсновныеНачисления.ДатаНачала КАК ДатаНачала,
		|	НачислениеЗарплатыОсновныеНачисления.ДатаОкончания КАК ДатаОкончания,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад,
		|	НачислениеЗарплатыОсновныеНачисления.Сотрудник.ОсновноеПодразделение КАК ОсновноеПодразделение
		|ИЗ
		|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(&ПериодРегистрации, ) КАК СведенияОСотрудникахСрезПоследних
		|		ПО НачислениеЗарплатыОсновныеНачисления.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
		|			И НачислениеЗарплатыОсновныеНачисления.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение
		|ГДЕ
		|	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ВыборкаДетальныеЗаписи.ВидРасчета;
		Движение.ПериодДействияНачало = ВыборкаДетальныеЗаписи.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(ВыборкаДетальныеЗаписи.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.БазовыйПериодНачало = ДобавитьМесяц(ПериодРегистрации, -2);
		Движение.БазовыйПериодКонец = НачалоМесяца(ПериодРегистрации) - 1;
		Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.Результат = 0;
		Движение.ЧасыФакт = 0;
		Если ВыборкаДетальныеЗаписи.ВидРасчета = ПредопределенноеЗначение("ПланВидовРасчета.ОсновныеНачисления.Коммандировка") Тогда
			Движение.ГрафикРаботы = Справочники.ГрафикиРаботы.Шестидневка;
			Движение.Подразделение = ВыборкаДетальныеЗаписи.ОсновноеПодразделение;
		Иначе
			Движение.Подразделение = ВыборкаДетальныеЗаписи.Подразделение;
			Движение.ГрафикРаботы = ВыборкаДетальныеЗаписи.ГрафикРаботы;
			Движение.Параметр1 	  = ВыборкаДетальныеЗаписи.Оклад;
		КонецЕсли;
		
	КонецЦикла;

	Дополнение = Движения.ОсновныеНачисления.ПолучитьДополнение();
	Для каждого Запись ИЗ Дополнение Цикл
		
		Движение = Движения.ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Запись);
		Движение.Сторно = Истина;
		Движение.ПериодРегистрации = Запись.ПериодРегистрацииСторно;
		Движение.ПериодДействияНачало = Запись.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Запись.ПериодДействияКонецСторно;
		
	КонецЦикла;
	
	Движения.Записать();
	РасчитатьКомандировку();
	РасчитатьОклад();
	
КонецПроцедуры

Процедура РасчитатьКомандировку()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЧасыФакт,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.ЧасыФактБаза, 0) КАК ЧасыФактБаза
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка) КАК ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Ссылка) КАК ОсновныеНачисленияБазаОсновныеНачисления
		|		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки";
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Измерения.Добавить("Подразделение");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	

	Для каждого Движение Из Движения.ОсновныеНачисления Цикл
	
		Если Движение.ВидРасчета <> ПланыВидовРасчета.ОсновныеНачисления.Коммандировка Тогда
		
			Продолжить;	
		
		КонецЕсли;
		
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			
			Если НЕ ЗначениеЗаполнено(Выборка.ЧасыФактБаза) Тогда
				Продолжить;
			КонецЕсли;
			
			Движение.Результат = Выборка.ЧасыФакт * Выборка.РезультатБаза / Выборка.ЧасыФактБаза;
			Движение.ЧасыФакт = Выборка.ЧасыФакт;
			
			Движение.Параметр1 = Выборка.РезультатБаза;
			Движение.Параметр2 = Выборка.ЧасыФактБаза;
			
			Движение.Результат = Движение.Результат * ?(Движение.Сторно, -1, 1);
			
		КонецЕсли;
		
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать();
	

КонецПроцедуры

Процедура РасчитатьОклад()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеДниПериодДействия КАК ДниПлан,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеДниФактическийПериодДействия КАК ДниФакт,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЧасыФакт
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка) КАК ОсновныеНачисленияДанныеГрафика";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Для каждого Движение Из Движения.ОсновныеНачисления Цикл
	
		Если Движение.ВидРасчета <> ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
		
			Продолжить;	
		
		КонецЕсли;
		
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			
			Если НЕ ЗначениеЗаполнено(Выборка.ДниФакт) Тогда
				Продолжить;
			КонецЕсли;
			Движение.Результат = Выборка.ДниФакт * Движение.Параметр1 / Выборка.ДниПлан;
			Движение.ЧасыФакт = Выборка.ЧасыФакт;
			
			Движение.Параметр2 = Выборка.ДниФакт;
			
			Движение.Результат = Движение.Результат * ?(Движение.Сторно, -1, 1);
			
		КонецЕсли;
		
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать(,,Ложь);
	

КонецПроцедуры
