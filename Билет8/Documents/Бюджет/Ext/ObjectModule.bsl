
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если КонецПериода <= НачалоПериода Тогда
		
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Конец периода меньше или равен началу периода!";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	// чтобы схема корректно работала для случаев, когда бюджет задаётся больше чем на месяц
	// бюджет нужно будет поделить по периоду (месяц)
	Движения.Бюджет.Записывать = Истина;
	
	ДлинаПериодаБюджета = (ДеньГода(КонецПериода) - ДеньГода(НачалоПериода)) + 1;
	
	Для Каждого ТекСтрокаСтатьиЗатрат Из СтатьиЗатрат Цикл
		
		лНачалоОтрезка = НачалоПериода;
		лКонецОтрезка = МИН(КонецПериода, КонецМесяца(НачалоПериода));
		
		ПериодКРаспределению = ДлинаПериодаБюджета;
		
		СуммаКРаспределению = ТекСтрокаСтатьиЗатрат.СуммаПлан;
		ДопустимоеПревышениеКРаспределению = ТекСтрокаСтатьиЗатрат.ДопустимоеПревышение;
		
		Пока СуммаКРаспределению <> 0 Цикл
			
			ДлинаОтрезка = (ДеньГода(лКонецОтрезка) - ДеньГода(лНачалоОтрезка)) + 1;
			
			СуммаПланОтрезка = ?(лКонецОтрезка = КонецПериода, СуммаКРаспределению, ДлинаОтрезка / ПериодКРаспределению * СуммаКРаспределению);
				
			ДопустимоеПревышениеОтрезка = ?(лКонецОтрезка = КонецПериода, ДопустимоеПревышениеКРаспределению, ДлинаОтрезка / ПериодКРаспределению * ДопустимоеПревышениеКРаспределению);
				
			Движение = Движения.Бюджет.Добавить();
			Движение.Период = лНачалоОтрезка;
			Движение.Подразделение = Подразделение;
			Движение.СтатьяЗатрат = ТекСтрокаСтатьиЗатрат.СтатьяЗатрат;
			Движение.СуммаПлан = СуммаПланОтрезка;
			Движение.ДопустимоеПревышение = ДопустимоеПревышениеОтрезка;
			
			СуммаКРаспределению = СуммаКРаспределению - Движение.СуммаПлан;
			ДопустимоеПревышениеКРаспределению = ДопустимоеПревышениеКРаспределению - Движение.ДопустимоеПревышение;
			
			ПериодКРаспределению = ПериодКРаспределению - ДлинаОтрезка;
			
			лНачалоОтрезка = лКонецОтрезка + 1;
			лКонецОтрезка = МИН(КонецПериода, КонецМесяца(лНачалоОтрезка));
			
		КонецЦикла;
		
	КонецЦикла;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	
КонецПроцедуры
