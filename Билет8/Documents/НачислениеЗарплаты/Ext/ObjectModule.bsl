
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаОсновныеНачисления Из ОсновныеНачисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаОсновныеНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = ТекСтрокаОсновныеНачисления.ДатаОкончания;
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		Движение.Подразделение = ТекСтрокаОсновныеНачисления.Подразделение;
	КонецЦикла;

	// регистр ДополнительныеНачисления
	Движения.ДополнительныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаДополнительныеНачисления Из ДополнительныеНачисления Цикл
		Движение = Движения.ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаДополнительныеНачисления.ВидРасчета;
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = ТекСтрокаДополнительныеНачисления.Сотрудник;
		Движение.Подразделение = ТекСтрокаДополнительныеНачисления.Подразделение;
		Движение.Результат = ТекСтрокаДополнительныеНачисления.Размер;
		Движение.РезультатUSD = ?(Курс = 0, 0, Движение.Результат / Курс);
	КонецЦикла;

	Движения.Записать();
	
	РассчитатьОклад();
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры

Процедура РассчитатьОклад()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК ЧасыФакт,
		|	ЕСТЬNULL(ШкалаЧасовойСтавки.Значение, 0) КАК ЧасоваяСтавка
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШкалаЧасовойСтавки КАК ШкалаЧасовойСтавки
		|		ПО ОсновныеНачисленияДанныеГрафика.Подразделение = ШкалаЧасовойСтавки.Подразделение
		|			И (ВЫБОР
		|				КОГДА ШкалаЧасовойСтавки.От ЕСТЬ NULL
		|					ТОГДА ЛОЖЬ
		|				ИНАЧЕ ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия >= ШкалаЧасовойСтавки.От
		|						И (ШкалаЧасовойСтавки.До = 0
		|							ИЛИ ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия < ШкалаЧасовойСтавки.До)
		|			КОНЕЦ)";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	

	Для каждого Движение Из Движения.ОсновныеНачисления Цикл
	
		Если Движение.ВидРасчета <> ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
		
			Продолжить;
		
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
		
			Движение.Параметр1 = ВыборкаДетальныеЗаписи.ЧасыФакт;
			Движение.Параметр2 = ВыборкаДетальныеЗаписи.ЧасоваяСтавка;
			
			Движение.Результат = ВыборкаДетальныеЗаписи.ЧасоваяСтавка * ВыборкаДетальныеЗаписи.ЧасыФакт;
			ДВижение.РезультатUSD = ?(Курс = 0, 0, Движение.Результат / Курс);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДВижения.ОсновныеНачисления.Записать();
	
КонецПроцедуры

