Процедура ПроведениеДокументаПоБюджету(Подразделение, СтатьяЗатрат, СуммаПоДокументу, Дата, Движения) Экспорт
	
	Движения.Бюджет.Записывать = Истина;
	Движение = Движения.Бюджет.Добавить();
	Движение.Период = Дата;
	Движение.Подразделение = Подразделение;
	Движение.СтатьяЗатрат = СтатьяЗатрат;
	Движение.СуммаФакт = СуммаПоДокументу;
	
	Движения.Бюджет.БлокироватьДляИзменения = Истина;
	Движения.Бюджет.Записать();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БюджетОбороты.СуммаПланОборот КАК СуммаПланОборот,
		|	БюджетОбороты.ДопустимоеПревышениеОборот КАК ДопустимоеПревышениеОборот,
		|	БюджетОбороты.СуммаФактОборот КАК СуммаФактОборот
		|ИЗ
		|	РегистрНакопления.Бюджет.Обороты(
		|			&НачалоМесяца,
		|			&КонецМесяца,
		|			,
		|			Подразделение = &Подразделение
		|				И СтатьяЗатрат = &СтатьяЗатрат) КАК БюджетОбороты";
	
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("СтатьяЗатрат", СтатьяЗатрат);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НеХватает = Выборка.СуммаФактОборот - Выборка.СуммаПланОборот;
		Если НеХватает > 0 Тогда
			
			НеХватаетСдопуском = Выборка.СуммаФактОборот - (Выборка.СуммаПланОборот + Выборка.ДопустимоеПревышениеОборот);
			Если НеХватаетСдопуском > 0 Тогда
				ТекстСообщения = СтрШаблон("Первышен бюджет (план + допуск) на %1", НеХватаетСдопуском);
			Иначе
				ТекстСообщения = СтрШаблон("Первышен бюджет (план) на %1", НеХватает);
			КонецЕсли;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Сообщить();
			
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры