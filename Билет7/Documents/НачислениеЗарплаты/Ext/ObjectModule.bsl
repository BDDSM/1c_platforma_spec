
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаОсновныеНачисления Из ОсновныеНачисления Цикл
		
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаОсновныеНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(ТекСтрокаОсновныеНачисления.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		Движение.Подразделение = ТекСтрокаОсновныеНачисления.Подразделение;
		Если ТекСтрокаОсновныеНачисления.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Коммандировка Тогда
			Движение.БазовыйПериодНачало = ДобавитьМесяц(ПериодРегистрации, -2);
			Движение.БазовыйПериодКонец = ПериодРегистрации-1;
			Движение.ГрафикиРаботы = Справочники.ГрафикиРаботы.Пятидневка;
		Иначе
			Движение.ГрафикиРаботы = ТекСтрокаОсновныеНачисления.ГрафикРаботы;
		КонецЕсли;
		
	КонецЦикла;

	Движения.Записать();
	
	РассчитатьКоммандировку();
	РассчитатьОклад();
	
КонецПроцедуры


Процедура РассчитатьКоммандировку()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	|	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
	|	ОсновныеНачисленияДанныеГрафика.ВидРасчета КАК ВидРасчета,
	|	ОсновныеНачисленияДанныеГрафика.ЗначениеБазовыйПериод КАК ЧасыПлан
	|ПОМЕСТИТЬ ВТ_ДанныеГрафика
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
	|			Регистратор = &Регистратор
	|				И ВидРасчета = &Командировка) КАК ОсновныеНачисленияДанныеГрафика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Подразделение,
	|	ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеГрафика.ЧасыПлан КАК ЧасыПлан,
	|	СУММА(ЕСТЬNULL(РабочееВремяСотрудников.Значение, 0)) КАК ЧасыФакт,
	|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК База
	|ИЗ
	|	ВТ_ДанныеГрафика КАК ВТ_ДанныеГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РабочееВремяСотрудников КАК РабочееВремяСотрудников
	|		ПО ВТ_ДанныеГрафика.Сотрудник = РабочееВремяСотрудников.Сотрудник
	|			И ВТ_ДанныеГрафика.Подразделение = РабочееВремяСотрудников.Подразделение
	|			И ВТ_ДанныеГрафика.ВидРасчета = РабочееВремяСотрудников.ВидРасчета
	|			И (РабочееВремяСотрудников.Период МЕЖДУ &НачалоМесяца И &КонецМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК ОсновныеНачисленияБазаОсновныеНачисления
	|		ПО ВТ_ДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеГрафика.ЧасыПлан,
	|	ВТ_ДанныеГрафика.НомерСтроки,
	|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0)";
	
	Запрос.УстановитьПараметр("Командировка", ПланыВидовРасчета.ОсновныеНачисления.Коммандировка);
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Измерения.Добавить("Подразделение");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	

	Для каждого Движение Из Движения.ОсновныеНачисления Цикл
	
		Если Движение.ВидРасчета <> ПланыВидовРасчета.ОсновныеНачисления.Коммандировка  Тогда
		
			Продолжить;	
		
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			
			Движение.Параметр1 = ВыборкаДетальныеЗаписи.ЧасыФакт;
			Движение.Параметр2 = ВыборкаДетальныеЗаписи.ЧасыПлан;
			Движение.Параметр3 = ВыборкаДетальныеЗаписи.База;
			
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ЧасыПлан) Тогда
				
				Движение.Результат = (ВыборкаДетальныеЗаписи.База / ВыборкаДетальныеЗаписи.ЧасыПлан) * ВыборкаДетальныеЗаписи.ЧасыФакт; 
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать();

КонецПроцедуры

Процедура РассчитатьОклад()


	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	|	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
	|	ОсновныеНачисленияДанныеГрафика.ВидРасчета КАК ВидРасчета,
	|	ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия КАК ЧасыПлан
	|ПОМЕСТИТЬ ВТ_ДанныеГрафика
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
	|			Регистратор = &Регистратор
	|				И ВидРасчета = &Командировка) КАК ОсновныеНачисленияДанныеГрафика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Подразделение,
	|	ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеГрафика.ЧасыПлан КАК ЧасыПлан,
	|	СУММА(ЕСТЬNULL(РабочееВремяСотрудников.Значение, 0)) КАК ЧасыФакт,
	|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад
	|ИЗ
	|	ВТ_ДанныеГрафика КАК ВТ_ДанныеГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РабочееВремяСотрудников КАК РабочееВремяСотрудников
	|		ПО ВТ_ДанныеГрафика.Сотрудник = РабочееВремяСотрудников.Сотрудник
	|			И ВТ_ДанныеГрафика.Подразделение = РабочееВремяСотрудников.Подразделение
	|			И ВТ_ДанныеГрафика.ВидРасчета = РабочееВремяСотрудников.ВидРасчета
	|			И (РабочееВремяСотрудников.Период МЕЖДУ &НачалоМесяца И &КонецМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
	|				&НачалоМесяца,
	|				(Сотрудник, Подразделение) В
	|					(ВЫБРАТЬ
	|						ВТ_ДанныеГрафика.Сотрудник КАК Сотрудник,
	|						ВТ_ДанныеГрафика.Подразделение КАК Подразделение
	|					ИЗ
	|						ВТ_ДанныеГрафика КАК ВТ_ДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
	|		ПО ВТ_ДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
	|			И ВТ_ДанныеГрафика.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеГрафика.ЧасыПлан,
	|	ВТ_ДанныеГрафика.НомерСтроки,
	|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0)";
	
	Запрос.УстановитьПараметр("Командировка", ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	

	Для каждого Движение Из Движения.ОсновныеНачисления Цикл
	
		Если Движение.ВидРасчета <> ПланыВидовРасчета.ОсновныеНачисления.Оклад  Тогда
		
			Продолжить;	
		
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			
			Движение.Параметр1 = ВыборкаДетальныеЗаписи.ЧасыФакт;
			Движение.Параметр2 = ВыборкаДетальныеЗаписи.ЧасыПлан;
			Движение.Параметр3 = ВыборкаДетальныеЗаписи.Оклад;
			
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ЧасыПлан) Тогда
				
				Движение.Результат = (ВыборкаДетальныеЗаписи.ЧасыФакт / ВыборкаДетальныеЗаписи.ЧасыПлан) * ВыборкаДетальныеЗаписи.Оклад; 
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать();

КонецПроцедуры


