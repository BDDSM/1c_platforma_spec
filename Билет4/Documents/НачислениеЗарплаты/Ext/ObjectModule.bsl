
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОсновныеНачисления.Записывать = Истина;
	Движения.ДополнительныеНачисления.Записывать = Истина;
	
	// регистр ОсновныеНачисления
	Для Каждого ТекСтрокаОсновныеНачисления Из ОсновныеНачисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаОсновныеНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(ТекСтрокаОсновныеНачисления.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		Движение.Подразделение = ТекСтрокаОсновныеНачисления.Подразделение;
		Движение.Смена = ТекСтрокаОсновныеНачисления.Смена;
	КонецЦикла;

	// регистр ДополнительныеНачисления
	Для Каждого ТекСтрокаДополнительныеНачисления Из ДополнительныеНачисления Цикл
		Движение = Движения.ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаДополнительныеНачисления.ВидРасчета;
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.БазовыйПериодНачало = НачалоМесяца(ПериодРегистрации-1);
		Движение.БазовыйПериодКонец = ПериодРегистрации-1;
		Движение.Сотрудник = ТекСтрокаДополнительныеНачисления.Сотрудник;
		Движение.Подразделение = ТекСтрокаДополнительныеНачисления.Подразделение;
		Движение.Параметр1 = ТекСтрокаДополнительныеНачисления.Размер;
	КонецЦикла;

	Движения.Записать();
	
	РасчитатьПоСтавкеЧаса();
	ПремияРуководителяОтБазы();
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	
КонецПроцедуры

Процедура РасчитатьПоСтавкеЧаса()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ОсновныеНачисления.ВидРасчета.ВидРасчета КАК ВидРасчета,
	|	ОсновныеНачисления.Смена КАК Смена
	|ПОМЕСТИТЬ ВТ_ОснНачисления
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.Регистратор = &Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки,
	|	Смена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОснНачисления.НомерСтроки КАК НомерСтроки,
	|	ВТ_ОснНачисления.ВидРасчета КАК ВидРасчета,
	|	ЕСТЬNULL(СтавкиСменСрезПоследних.Ставка, 0) КАК Ставка,
	|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК ЧасыФакт
	|ИЗ
	|	ВТ_ОснНачисления КАК ВТ_ОснНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ОсновныеНачисленияДанныеГрафика
	|		ПО ВТ_ОснНачисления.НомерСтроки = ОсновныеНачисленияДанныеГрафика.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиСмен.СрезПоследних(
	|				&ПериодРегистрации,
	|				Смена В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ВТ_ОснНачисления.Смена КАК Смена
	|					ИЗ
	|						ВТ_ОснНачисления КАК ВТ_ОснНачисления)) КАК СтавкиСменСрезПоследних
	|		ПО ВТ_ОснНачисления.Смена = СтавкиСменСрезПоследних.Смена";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого Движение из Движения.ОсновныеНачисления Цикл
		
		ВыборкаДетальныеЗаписи.Сбросить();
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			
			Если ВыборкаДетальныеЗаписи.ВидРасчета <> Перечисления.ВидыРасчета.ПоСтавкеЧаса Тогда
				Продолжить;
			КонецЕсли;
			
			Движение.Результат = ВыборкаДетальныеЗаписи.Ставка * ВыборкаДетальныеЗаписи.ЧасыФакт;
			Движение.Параметр1 = ВыборкаДетальныеЗаписи.Ставка;
			Движение.Параметр2 = ВыборкаДетальныеЗаписи.ЧасыФакт;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать();
	
КонецПроцедуры

Процедура ПремияРуководителяОтБазы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ДополнительныеНачисления.ВидРасчета.ВидРасчета КАК ВидРасчета,
	|	ДополнительныеНачисления.Подразделение.Руководитель КАК РуководительПодразделения,
	|	ДополнительныеНачисления.Параметр1 КАК Параметр1
	|ПОМЕСТИТЬ ВТ_ДопНачисления
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
	|ГДЕ
	|	ДополнительныеНачисления.Регистратор = &Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДопНачисления.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДопНачисления.ВидРасчета КАК ВидРасчета,
	|	ВТ_ДопНачисления.РуководительПодразделения КАК РуководительПодразделения,
	|	СУММА(ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0)) КАК РезультатБаза,
	|	ВТ_ДопНачисления.Параметр1 КАК Параметр1
	|ИЗ
	|	ВТ_ДопНачисления КАК ВТ_ДопНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, &РазрезПоСотруднику, регистратор = &Регистратор) КАК ДополнительныеНачисленияБазаОсновныеНачисления
	|		ПО ВТ_ДопНачисления.НомерСтроки = ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки
	|ГДЕ
	|	ДополнительныеНачисленияБазаОсновныеНачисления.СотрудникРазрез <> ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДопНачисления.НомерСтроки,
	|	ВТ_ДопНачисления.ВидРасчета,
	|	ВТ_ДопНачисления.РуководительПодразделения,
	|	ВТ_ДопНачисления.Параметр1";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Подразделение");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	РазрезПоСотруднику = Новый Массив;
	РазрезПоСотруднику.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("РазрезПоСотруднику", РазрезПоСотруднику);
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого Движение из Движения.ДополнительныеНачисления Цикл
		
		ВыборкаДетальныеЗаписи.Сбросить();
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			
			Если ВыборкаДетальныеЗаписи.ВидРасчета <> Перечисления.ВидыРасчета.ПремияРуководителяОтБазы Тогда
				Продолжить;
			КонецЕсли;
			
			Если Движение.Сотрудник = ВыборкаДетальныеЗаписи.РуководительПодразделения Тогда
				
				Движение.Результат = (ВыборкаДетальныеЗаписи.Параметр1 / 100) * ВыборкаДетальныеЗаписи.РезультатБаза;
				Движение.Параметр2 = ВыборкаДетальныеЗаписи.РезультатБаза;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ДополнительныеНачисления.Записать();

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ПериодРегистрации = НачалоМесяца(ПериодРегистрации);
КонецПроцедуры
