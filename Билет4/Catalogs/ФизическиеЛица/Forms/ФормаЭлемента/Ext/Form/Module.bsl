
&НаКлиенте
Процедура ВыбратьФотографию(Команда)

	ОбработкаОповещения = Новый ОписаниеОповещения
       ("ВыбратьФотографиюЗавершение", ЭтотОбъект);

	ПоказатьВопрос(ОбработкаОповещения, "Сохранить только путь к файлу?", РежимДиалогаВопрос.ДаНет);
	   
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФотографиюЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
        Возврат;
    КонецЕсли;

	ТолькоПуть = (Результат = КодВозвратаДиалога.Да);
	
	ОбработкаОкончанияПомещения = Новый ОписаниеОповещения
       ("ОбработчикОкончанияПомещения", ЭтотОбъект);

    НачатьПомещениеФайла(ОбработкаОкончанияПомещения, , , Истина,     
        ЭтотОбъект.УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОкончанияПомещения(Результат, Адрес, 
        ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
		
	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;
	
	Фотография = Адрес;
	Объект.АдресКартинки = ВыбранноеИмяФайла;
		
	Модифицированность = Истина;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОкончанияОткрытия(Результат, Адрес, 
        ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
		
	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;
	
	Фотография = Адрес;
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ТолькоПуть Тогда
		ТекущийОбъект.Изображение = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Фотография));
		ТекущийОбъект.АдресКартинки = "";
		
	Иначе
		ТекущийОбъект.Изображение = Новый ХранилищеЗначения(Новый Картинка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.АдресКартинки) Тогда
		Фотография = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Изображение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Объект.АдресКартинки) Тогда
		
		ТолькоПуть = Истина;
		
		ОбработчикОкончанияОткрытия = Новый ОписаниеОповещения
			("ОбработчикОкончанияОткрытия", ЭтотОбъект);
		Адрес = Объект.АдресКартинки;
		НачатьПомещениеФайла(ОбработчикОкончанияОткрытия, , Адрес, ложь,     
			ЭтотОбъект.УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры



