
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.Управленческий.Записывать = Истина;
	Движения.Управленческий.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.ТоварыВПути);
	ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Отправление, Отправление);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УправленческийОстатки.Субконто1.СкладПолучатель КАК СкладПолучатель,
		|	УправленческийОстатки.Субконто2 КАК Номенклатура,
		|	МАКСИМУМ(УправленческийОстатки.КоличествоОстаток) КАК Количество,
		|	СУММА(УправленческийОстатки.СуммаОстаток) КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Остатки(&МоментВремени, Счет = &СчетТоварыВПути, &СубконтоОтправлениеНоменклатура, Субконто1 = &Отправление) КАК УправленческийОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	УправленческийОстатки.Субконто2,
		|	УправленческийОстатки.Субконто1.СкладПолучатель";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Отправление", Отправление);
	СубконтоОтправлениеНоменклатура = Новый Массив;
	СубконтоОтправлениеНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Отправление);
	СубконтоОтправлениеНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	Запрос.УстановитьПараметр("СубконтоОтправлениеНоменклатура", СубконтоОтправлениеНоменклатура);
	Запрос.УстановитьПараметр("СчетТоварыВПути", ПланыСчетов.Управленческий.ТоварыВПути);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.Управленческий.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
		Движение.СчетКт = ПланыСчетов.Управленческий.ТоварыВПути;
		Движение.Период = Дата;
		Движение.Сумма = Выборка.Сумма;
		Движение.КоличествоДт = Выборка.Количество;
		Движение.КоличествоКт = Выборка.Количество;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склад] = Выборка.СкладПолучатель;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Отправление] = Отправление;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Дата] = Дата;
		
	КонецЦикла;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
