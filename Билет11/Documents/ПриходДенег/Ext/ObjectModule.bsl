
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ЗадолженностиПокупателей.Записывать = Истина;
	Движения.ЗадолженностиПокупателей.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗадолженностиПокупателей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокДокументов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Отгрузка");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходДенегДокументы.Отгрузка КАК Отгрузка
		|ПОМЕСТИТЬ ВТ_ДанныеДок
		|ИЗ
		|	Документ.ПриходДенег.СписокДокументов КАК ПриходДенегДокументы
		|ГДЕ
		|	ПриходДенегДокументы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеДок.Отгрузка КАК Отгрузка,
		|	ЕСТЬNULL(ЗадолженностиПокупателейОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	ВТ_ДанныеДок КАК ВТ_ДанныеДок
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗадолженностиПокупателей.Остатки(
		|				&МоментВремени,
		|				Документ В
		|					(ВЫБРАТЬ
		|						ВТ_ДанныеДок.Отгрузка КАК Отгрузка
		|					ИЗ
		|						ВТ_ДанныеДок КАК ВТ_ДанныеДок)) КАК ЗадолженностиПокупателейОстатки
		|		ПО ВТ_ДанныеДок.Отгрузка = ЗадолженностиПокупателейОстатки.Документ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_ДанныеДок.Отгрузка.МоментВремени
		|ИТОГИ
		|	СУММА(СуммаОстаток)
		|ПО
		|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщийИтог.Следующий();
	
	Если СуммаПоДокументу > ВыборкаОбщийИтог.СуммаОстаток Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Сумма по документу %1 больче чем сумма задолженности %2", СуммаПоДокументу, ВыборкаОбщийИтог.СуммаОстаток);
		Сообщение.Сообщить();
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;

	Выборка = ВыборкаОбщийИтог.Выбрать();
	
	СуммаКРаспределению = СуммаПоДокументу;
	Пока СуммаКРаспределению > 0 И Выборка.Следующий() Цикл
		
		СуммаКСписанию = МИН(СуммаКРаспределению, Выборка.СуммаОстаток);
		СуммаКРаспределению = СуммаКРаспределению - СуммаКСписанию;
		
		Движение = Движения.ЗадолженностиПокупателей.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Документ = Выборка.Отгрузка;
		Движение.Сумма = СуммаКСписанию;
	КонецЦикла;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	
КонецПроцедуры
