
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Отдел = ТорговаяТочка;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
	КонецЦикла;
	
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК КоличествоДок
		|ПОМЕСТИТЬ ВТ_ДанныеДок
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоДок
		|ПОМЕСТИТЬ ВТ_ДанныеДляПеремещения
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&Граница,
		|			Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ_ДанныеДок.Номенклатура
		|					ИЗ
		|						ВТ_ДанныеДок)
		|				И Отдел = &ТорговаяТочка) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени()));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ТорговаяТочка", ТорговаяТочка);
	
	Запрос.Выполнить();
	
	РезультатЗапроса = Мвт.Таблицы.Найти("ВТ_ДанныеДляПеремещения").ПолучитьДанные();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		СоздатьДокументыПеремещения(МВТ, Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда 
		СоздатьДвиженияПоРегиструСебестоимости(Движения, ТорговаяТочка, МВТ, Отказ);
	КонецЕсли;
	

	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры

Процедура СоздатьДвиженияПоРегиструСебестоимости(Движения, Партия, МВТ, Отказ)
	
	Движения.Себестоимость.Записывать = Истина;
	Движения.Себестоимость.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Себестоимость");
	ЭлементБлокировки.УстановитьЗначение("Партия", Партия);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТ_ДанныеДок.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ВТ_ДанныеДок.КоличествоДок) КАК КоличествоДок,
	|	СУММА(ЕСТЬNULL(СебестоимостьОстатки.КоличествоОстаток, 0)) КАК КоличествоОстаток,
	|	СУММА(ЕСТЬNULL(СебестоимостьСумма.КоличествоОстаток, 0)) КАК КоличествоОстатокСебестоимость,
	|	СУММА(ЕСТЬNULL(СебестоимостьСумма.СуммаОстаток, 0)) КАК СуммаОстаток,
	|	ВТ_ДанныеДок.Номенклатура.Представление КАК НоменклатураПредставление
	|ИЗ
	|	ВТ_ДанныеДок КАК ВТ_ДанныеДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Себестоимость.Остатки(
	|				&МоментВремени,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							ВТ_ДанныеДок.Номенклатура
	|						ИЗ
	|							ВТ_ДанныеДок)
	|					И Партия = &ОтделЗакупок) КАК СебестоимостьОстатки
	|		ПО ВТ_ДанныеДок.Номенклатура = СебестоимостьОстатки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Себестоимость.Остатки(
	|				&МоментВремени,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							ВТ_ДанныеДок.Номенклатура
	|						ИЗ
	|							ВТ_ДанныеДок)
	|					И Партия.ВидПодразделения = &ВидОтделЗакупок) КАК СебестоимостьСумма
	|		ПО ВТ_ДанныеДок.Номенклатура = СебестоимостьОстатки.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДок.Номенклатура,
	|	ВТ_ДанныеДок.Номенклатура.Представление";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());

	Запрос.УстановитьПараметр("ОтделЗакупок", Партия);
	Запрос.УстановитьПараметр("ВидОтделЗакупок", Перечисления.ВидыПодразделений.ТорговаяТочка);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписи.КоличествоДок > ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
			
			Отказ = Истина;
			Сообщить("Не хватает партий товара " + ВыборкаДетальныеЗаписи.НоменклатураПредставление + " для перемещения в торговую точку! Не хватает в кол-ве: " + (ВыборкаДетальныеЗаписи.КоличествоДок - ВыборкаДетальныеЗаписи.КоличествоОстаток));
			Продолжить;
			
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			
			КоличествоОтдела = МИН(ВыборкаДетальныеЗаписи.КоличествоДок, ВыборкаДетальныеЗаписи.КоличествоОстаток);
			СуммаОтдела = ?(КоличествоОтдела = ВыборкаДетальныеЗаписи.КоличествоОстаток,   
				ВыборкаДетальныеЗаписи.СуммаОстаток, 
				ВыборкаДетальныеЗаписи.СуммаОстаток / ВыборкаДетальныеЗаписи.КоличествоОстатокСебестоимость * КоличествоОтдела);
			
			Движение = Движения.Себестоимость.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Партия = Партия;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			
			Движение.Количество = КоличествоОтдела;
			Движение.Сумма = СуммаОтдела;
			
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура СоздатьДокументыПеремещения(МВТ, Отказ)
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТ_ДанныеДляПеремещения.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДляПеремещения.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ВТ_ДанныеДляПеремещения.КоличествоДок КАК КоличествоДок,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.Отдел, """") КАК Отдел,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.Отдел.Приоритет, 0) КАК ПриоритетОтдела
	|ИЗ
	|	ВТ_ДанныеДляПеремещения КАК ВТ_ДанныеДляПеремещения,
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&МоментВремени,
	|			Отдел.ВидПодразделения = &ВидПодразделения
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						ВТ_ДанныеДляПеремещения.Номенклатура
	|					ИЗ
	|						ВТ_ДанныеДляПеремещения)) КАК ОстаткиНоменклатурыОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПриоритетОтдела УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(КоличествоДок),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("ВидПодразделения", Перечисления.ВидыПодразделений.ОтделЗакупок);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ДокументыПеремещения = Новый Соответствие;
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.КоличествоДок > ВыборкаНоменклатура.КоличествоОстаток Тогда
			
			Отказ = Истина;
			Сообщить("Не хватает товара " + ВыборкаНоменклатура.НоменклатураПредставление + " в количестве: " + (ВыборкаНоменклатура.КоличествоДок - ВыборкаНоменклатура.КоличествоОстаток) + " для перемещения в торговую точку");
			Продолжить;
			
		КонецЕсли; 
		
		Если НЕ Отказ Тогда
			
			КоличествоДляПеремещения = ВыборкаНоменклатура.КоличествоДок;
			
			ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
			
			Пока КоличествоДляПеремещения > 0 И ВыборкаДетальныеЗаписи.Следующий() Цикл
				ДокументПеремещения = ДокументыПеремещения.Получить(ВыборкаДетальныеЗаписи.Отдел);	
				
				Если ДокументПеремещения = Неопределено Тогда
					ДокументПеремещения = Документы.Перемещение.СоздатьДокумент();
					ДокументПеремещения.Дата = Дата - 1;
					ДокументПеремещения.ТорговаяТочка = ТорговаяТочка;
					ДокументПеремещения.ОтделЗакупа = ВыборкаДетальныеЗаписи.Отдел;
					ДокументПеремещения.Основание = Ссылка;
					ДокументыПеремещения.Вставить(ВыборкаДетальныеЗаписи.Отдел, ДокументПеремещения);
				КонецЕсли;
				
				НовСтр = ДокументПеремещения.СписокНоменклатуры.Добавить();
				НовСтр.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				НовСтр.Количество = МИН(КоличествоДляПеремещения, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				
				КоличествоДляПеремещения = КоличествоДляПеремещения - НовСтр.Количество;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	Если Не Отказ Тогда
		Для каждого Документ Из ДокументыПеремещения Цикл
			
			Документ.Значение.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

