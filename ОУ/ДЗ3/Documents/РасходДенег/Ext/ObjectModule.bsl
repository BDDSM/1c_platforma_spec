
Процедура ОбработкаПроведения(Отказ, Режим)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходДенегВзаиморасчеты.Договор КАК Договор,
	|	РасходДенегВзаиморасчеты.Номенклатура КАК Номенклатура,
	|	СУММА(РасходДенегВзаиморасчеты.Количество) КАК КоличествоДок,
	|	СУММА(РасходДенегВзаиморасчеты.СуммаВыплаты) КАК СуммаВыплатыДок,
	|	СУММА(РасходДенегВзаиморасчеты.СуммаПродажи) КАК СуммаПродажиДок
	|ПОМЕСТИТЬ ВТ_ДанныеДок
	|ИЗ
	|	Документ.РасходДенег.Взаиморасчеты КАК РасходДенегВзаиморасчеты
	|ГДЕ
	|	РасходДенегВзаиморасчеты.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходДенегВзаиморасчеты.Договор,
	|	РасходДенегВзаиморасчеты.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Выполнить();
	
	РезультатЗапроса = МенеджерВременныхТаблиц.Таблицы[0].ПолучитьДанные();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	// регистр ВзаиморасчетыСКоммитентом Расход
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВзаиморасчетыСКоммитентом.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Договор = ВыборкаДетальныеЗаписи.Договор;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоДок;
		Движение.СуммаПродажи = ВыборкаДетальныеЗаписи.СуммаПродажиДок;
		Движение.СуммаВыплаты = ВыборкаДетальныеЗаписи.СуммаВыплатыДок;
	КонецЦикла;
	
	Движения.ВзаиморасчетыСКоммитентом.БлокироватьДляИзменения = Истина;
	Движения.ВзаиморасчетыСКоммитентом.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТ_ДанныеДок.Договор.Представление КАК ДоговорПредставление,
		|	ВТ_ДанныеДок.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ВзаиморасчетыСКоммитентомОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ВзаиморасчетыСКоммитентомОстатки.СуммаПродажиОстаток КАК СуммаПродажиОстаток,
		|	ВзаиморасчетыСКоммитентомОстатки.СуммаВыплатыОстаток КАК СуммаВыплатыОстаток
		|ИЗ
		|	ВТ_ДанныеДок КАК ВТ_ДанныеДок
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыСКоммитентом.Остатки(
		|				&Граница,
		|				Договор В
		|						(ВЫБРАТЬ
		|							ВТ_ДанныеДок.Договор
		|						ИЗ
		|							ВТ_ДанныеДок)
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							ВТ_ДанныеДок.Номенклатура
		|						ИЗ
		|							ВТ_ДанныеДок)) КАК ВзаиморасчетыСКоммитентомОстатки
		|		ПО ВТ_ДанныеДок.Номенклатура = ВзаиморасчетыСКоммитентомОстатки.Номенклатура
		|			И ВТ_ДанныеДок.Договор = ВзаиморасчетыСКоммитентомОстатки.Договор
		|ГДЕ
		|	(ВзаиморасчетыСКоммитентомОстатки.КоличествоОстаток < 0
		|			ИЛИ ВзаиморасчетыСКоммитентомОстатки.СуммаПродажиОстаток < 0
		|			ИЛИ ВзаиморасчетыСКоммитентомОстатки.СуммаВыплатыОстаток < 0)";
	
	ЗАпрос.УстановитьПараметр("Ссылка", Ссылка);
	ЗАпрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени()));
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Отказ = Истина;
		Сообщить("По договору " + ВыборкаДетальныеЗаписи.ДоговорПредставление + " есть ошибка с товаром: " + ВыборкаДетальныеЗаписи.НоменклатураПредставление + "
		| количество: " + ВыборкаДетальныеЗаписи.КоличествоОстаток + "; сумма продажи: " + ВыборкаДетальныеЗаписи.СуммаПродажиОстаток + "; сумма выплаты: " + ВыборкаДетальныеЗаписи.СуммаВыплатыОстаток);
	КонецЦикла;
	
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
