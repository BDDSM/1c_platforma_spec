
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	// регистр ОстаткиНоменклатуры Приход
	ЗначениеУчетнойПолитики = УчетнаяПолитикаСервер.ПолучитьТекущуюУчетнуюПолитику(Дата);
	
	ВидНоменклатурыУслуга = Перечисления.ВидыНоменклатуры.Услуга;

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);

	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК КоличествоДок,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК СуммаДок
	|ПОМЕСТИТЬ Вт_Номенклатура
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вт_Номенклатура.Номенклатура КАК Номенклатура,
	|	Вт_Номенклатура.Номенклатура.Представление КАК НоменклатураПредставление,
	|	Вт_Номенклатура.КоличествоДок КАК КоличествоДок,
	|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	Вт_Номенклатура.СуммаДок КАК СуммаДок,
	|	Вт_Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
	|ИЗ
	|	Вт_Номенклатура КАК Вт_Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МоментВремени,
	|				Склад = &Склад
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							Вт_Номенклатура.Номенклатура
	|						ИЗ
	|							Вт_Номенклатура)) КАК ОстаткиНоменклатурыОстатки
	|		ПО Вт_Номенклатура.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(КоличествоДок),
	|	СУММА(КоличествоОстаток),
	|	СУММА(СуммаОстаток),
	|	МАКСИМУМ(СуммаДок),
	|	МАКСИМУМ(ВидНоменклатуры)
	|ПО
	|	Номенклатура";
	
	Если ЗначениеУчетнойПолитики = ПредопределенноеЗначение("Перечисление.УчетнаяПолитика.ФИФО") Тогда
	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Партия.МоментВремени УБЫВ", "Партия.МоментВремени");
		
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатурыУслуга);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.ВидНоменклатуры <> ВидНоменклатурыУслуга И ВыборкаНоменклатура.КоличествоДок > ВыборкаНоменклатура.КоличествоОстаток Тогда
			
			Сообщить("Не хватает номенклатуры: " + ВыборкаНоменклатура.НоменклатураПредставление + " в количестве: " + (ВыборкаНоменклатура.КоличествоДок - ВыборкаНоменклатура.КоличествоОстаток));
			Отказ = Истина;
			Продолжить;
		КонецЕсли; 
		Себестоимость = 0;
		
		Если НЕ Отказ Тогда
			Если ВыборкаНоменклатура.ВидНоменклатуры <> ВидНоменклатурыУслуга Тогда
				ОсталосьСписать = ВыборкаНоменклатура.КоличествоДок;
				ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ОсталосьСписать > 0 И ВыборкаДетальныеЗаписи.Следующий() Цикл
					
					Движение = Движения.ОстаткиНоменклатуры.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
					Движение.Склад = Склад;
					Если ЗначениеУчетнойПолитики <> ПредопределенноеЗначение("Перечисление.УчетнаяПолитика.Средняя") Тогда
						
						Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
						
					КонецЕсли; 
					Движение.Количество = МИН(ОсталосьСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток); //КоличествоСписания;
					
					СуммаСписания = ?(Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток, 
					ВыборкаДетальныеЗаписи.СуммаОстаток,
					ВыборкаДетальныеЗаписи.СуммаОстаток / ВыборкаДетальныеЗаписи.КоличествоОстаток * Движение.Количество);
					
					Движение.Сумма = СуммаСписания;
					
					Себестоимость = Себестоимость + СуммаСписания;
					ОсталосьСписать = ОсталосьСписать - Движение.Количество;
					
				КонецЦикла;
			КонецЕсли;
			// регистр ПродажиКомпании 
			Движение = Движения.ПродажиКомпании.Добавить();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
			Движение.Количество = ВыборкаНоменклатура.КоличествоДок;
			Движение.Сумма = ВыборкаНоменклатура.СуммаДок;
			Движение.Себестоимость = Себестоимость;
		КонецЕсли; 
	КонецЦикла;
	
	Движения.ПродажиКомпании.БлокироватьДляИзменения = Истина;
	Движения.ПродажиКомпании.Записать();
	
	
	//Движения.ПродажиКомпании.Записывать = Истина;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
