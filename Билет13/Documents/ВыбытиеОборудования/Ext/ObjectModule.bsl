
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.НоменклатураВЭксплуатации.Записывать = Истина;
	Движения.НоменклатураВЭксплуатации.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НоменклатураВЭксплуатации");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураВЭксплуатацииОстатки.Номенклатура КАК Номенклатура,
	|	НоменклатураВЭксплуатацииОстатки.СрокГодности КАК СрокГодности,
	|	НоменклатураВЭксплуатацииОстатки.СрокЭксплуатации КАК СрокЭксплуатации,
	|	НоменклатураВЭксплуатацииОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	НоменклатураВЭксплуатацииОстатки.СуммаОстаток КАК СуммаОстаток,
	|	ЕСТЬNULL(СебестоимостьНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоВсего,
	|	ЕСТЬNULL(СебестоимостьНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаВсего
	|ИЗ
	|	РегистрНакопления.НоменклатураВЭксплуатации.Остатки(
	|			&МоментВремени,
	|			СрокГодности <= &СрокГодности
	|				ИЛИ (СрокЭксплуатации <> ДАТАВРЕМЯ(1,1,1) И СрокЭксплуатации <= &СрокЭксплуатации)) КАК НоменклатураВЭксплуатацииОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьНоменклатуры.Остатки(&МоментВремени, ) КАК СебестоимостьНоменклатурыОстатки
	|		ПО НоменклатураВЭксплуатацииОстатки.Номенклатура = СебестоимостьНоменклатурыОстатки.Номенклатура
	|ИТОГИ
	|	МАКСИМУМ(КоличествоВсего),
	|	МАКСИМУМ(СуммаВсего)
	|ПО
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("СрокГодности", Дата);
	Запрос.УстановитьПараметр("СрокЭксплуатации", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		Списано = 0;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Движение = Движения.НоменклатураВЭксплуатации.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.СрокГодности = ВыборкаДетальныеЗаписи.СрокГодности;
			Движение.СрокЭксплуатации = ВыборкаДетальныеЗаписи.СрокЭксплуатации;
			Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток;

			Списано = Списано + ВыборкаДетальныеЗаписи.КоличествоОстаток;
			
		КонецЦикла;
		
		Себестоимость = ?(Списано = ВыборкаНоменклатура.КоличествоВсего, 
			ВыборкаНоменклатура.СуммаВсего,
			Списано * ВыборкаНоменклатура.СуммаВсего / ВыборкаНоменклатура.КоличествоВсего); 
		
		Движение = Движения.СебестоимостьНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
		Движение.Количество = Списано;
		Движение.Сумма = Себестоимость;
		
	КонецЦикла;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
