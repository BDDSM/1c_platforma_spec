
&НаСервере
Процедура СформироватьДиаграммуНаСервере()
	
	ДиаграммаГанта.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияФактическийПериодДействия.ПериодДействияНачало КАК ПериодДействияНачало,
		|	ОсновныеНачисленияФактическийПериодДействия.ПериодДействияКонец КАК ПериодДействияКонец,
		|	ОсновныеНачисленияФактическийПериодДействия.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисленияФактическийПериодДействия.Результат КАК Результат,
		|	ОсновныеНачисленияФактическийПериодДействия.Регистратор КАК Регистратор,
		|	ОсновныеНачисленияФактическийПериодДействия.Регистратор.Представление КАК РегистраторПредставление,
		|	ОсновныеНачисленияФактическийПериодДействия.ВидРасчета КАК ВидРасчета
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ФактическийПериодДействия(ПериодДействия МЕЖДУ &НачалоПериода И &КонецПериода) КАК ОсновныеНачисленияФактическийПериодДействия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДополнительныеНачисления.ПериодРегистрации,
		|	ДополнительныеНачисления.ПериодРегистрации,
		|	ДополнительныеНачисления.Сотрудник,
		|	ДополнительныеНачисления.Результат,
		|	ДополнительныеНачисления.Регистратор,
		|	ДополнительныеНачисления.Регистратор.Представление,
		|	ДополнительныеНачисления.ВидРасчета
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
		|ГДЕ
		|	ДополнительныеНачисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДополнительныеНачисления.Активность";
	
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("НачалоПериода", период.ДатаНачала);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Точка = ДиаграммаГанта.УстановитьТочку(Выборка.Сотрудник);
		
		Серия = ДиаграммаГанта.УстановитьСерию(Выборка.ВидРасчета);
		
		Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
		
		ВидРасчета = Выборка.ВидРасчета;
		
		Интервал = Значение.Добавить();
		Интервал.Начало = Выборка.ПериодДействияНачало;
		Если ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.Премия Тогда
			Интервал.Конец = КонецМесяца(Выборка.ПериодДействияКонец);
		Иначе
			Интервал.Конец = Выборка.ПериодДействияКонец;
		КонецЕсли;
		Интервал.Текст = Выборка.РегистраторПредставление;
		Интервал.Расшифровка = Выборка.Регистратор;
		
		Если ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
			Серия.Цвет = WebЦвета.Зеленый;
		ИначеЕсли ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Больничный Тогда
			Серия.Цвет = WebЦвета.Красный;
		Иначе 
			Серия.Цвет = WebЦвета.Синий;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДиаграмму(Команда)
	СформироватьДиаграммуНаСервере();
КонецПроцедуры
