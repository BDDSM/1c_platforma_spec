
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиНоменклатуры.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Характеристика КАК Характеристика,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	ВЫБОР
		|		КОГДА РасходнаяНакладнаяСписокНоменклатуры.Номенклатура ССЫЛКА Справочник.НаборыНоменклатуры
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоНабор
		|ПОМЕСТИТЬ ВТ_ДанныеДок
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Характеристика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставНабораСрезПоследних.НаборНоменклатуры КАК НаборНоменклатуры,
		|	ИзменениеСоставаНабораСостав.Номенклатура КАК Номенклатура,
		|	ИзменениеСоставаНабораСостав.Характеристика КАК Характеристика,
		|	ИзменениеСоставаНабораСостав.Количество КАК Количество
		|ПОМЕСТИТЬ ВТ_СоставыНаборов
		|ИЗ
		|	РегистрСведений.СоставНабора.СрезПоследних(
		|			&МоментВремени,
		|			НаборНоменклатуры В
		|				(ВЫБРАТЬ
		|					ВТ_ДанныеДок.Номенклатура КАК Номенклатура
		|				ИЗ
		|					ВТ_ДанныеДок КАК ВТ_ДанныеДок
		|				ГДЕ
		|					ВТ_ДанныеДок.ЭтоНабор)) КАК СоставНабораСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеСоставаНабора.Состав КАК ИзменениеСоставаНабораСостав
		|		ПО СоставНабораСрезПоследних.СоставНабора = ИзменениеСоставаНабораСостав.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НаборНоменклатуры,
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ_ДанныеДляСписания
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТ_ДанныеДок.Номенклатура КАК Номенклатура,
		|		ВТ_ДанныеДок.Характеристика КАК Характеристика,
		|		ВТ_ДанныеДок.Количество КАК Количество
		|	ИЗ
		|		ВТ_ДанныеДок КАК ВТ_ДанныеДок
		|	ГДЕ
		|		НЕ ВТ_ДанныеДок.ЭтоНабор
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВТ_СоставыНаборов.Номенклатура,
		|		ВТ_СоставыНаборов.Характеристика,
		|		ВТ_СоставыНаборов.Количество * ВТ_ДанныеДок.Количество
		|	ИЗ
		|		ВТ_ДанныеДок КАК ВТ_ДанныеДок
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СоставыНаборов КАК ВТ_СоставыНаборов
		|			ПО ВТ_ДанныеДок.Номенклатура = ВТ_СоставыНаборов.НаборНоменклатуры
		|	ГДЕ
		|		ВТ_ДанныеДок.ЭтоНабор) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеДляСписания.Номенклатура КАК Номенклатура,
		|	ВТ_ДанныеДляСписания.Характеристика КАК Характеристика,
		|	ВТ_ДанныеДляСписания.Количество КАК Количество,
		|	&Период КАК Период,
		|	&ВидДвижения КАК ВидДвижения
		|ИЗ
		|	ВТ_ДанныеДляСписания КАК ВТ_ДанныеДляСписания";
	
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ОстаткиНоменклатуры.Загрузить(РезультатЗапроса.Выгрузить());
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	
	Движения.ОстаткиНоменклатуры.Записать();
	
	
	Запрос.Текст = "ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ОстаткиНоменклатурыОстатки.Характеристика КАК Характеристика,
		|	ОстаткиНоменклатурыОстатки.Характеристика.Представление КАК ХарактеристикаПредставление,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&Граница,
		|			(Номенклатура, Характеристика) В
		|				(ВЫБРАТЬ
		|					ВТ_ДанныеДляСписания.Номенклатура КАК Номенклатура,
		|					ВТ_ДанныеДляСписания.Характеристика КАК Характеристика
		|				ИЗ
		|					ВТ_ДанныеДляСписания КАК ВТ_ДанныеДляСписания)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока выборка.Следующий() Цикл
			
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает товара: %1 / %2 в количестве: %3", Выборка.НоменклатураПредставление, Выборка.ХарактеристикаПредставление, Выборка.КоличествоОстаток);
			Сообщение.Сообщить();
			
		КонецЦикла;
		
	КонецЕсли;

	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	
КонецПроцедуры
